newline ::= "\n", {"\n"}

arithmetic_operator ::= "+" | "/" | "%" | "*" | "^" | "-"

comparison_operator ::= "==" | ">" | ">=" | "<" | "<=" | "!="

logical_operator ::= "||" | "&&"

logical_not ::= "!"

lowercase_letter ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"

uppercase_letter ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

letter ::= lowercase_letter | uppercase_letter

digit_except_zero ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

digit ::= "0" | digit_except_zero

integer ::= ["-"], ("0" | (digit_except_zero, {digit}))

double ::= integer, ".", digit, {digit}

number ::= integer | double

string ::= "\"", {any symbol}, "\""

boolean ::= "true" | "false"

raw_data_type ::= "int" | "double" | "string" | "bool"

data_type ::= raw_data_type, {"[]"}

identifier ::= letter | "_", {letter | digit | "_"}

assignment ::= identifier, "=", expression, ["please"]

definition ::= "def", identifier, ":", data_type, ["please"]

definition_and_assignment ::= "def", identifier, [":", data_type], "=", expression, ["please"]

logical_expression ::= expression, logical_operator, expression | logical_not, expression

comparison_expression ::= expression, comparison_operator, expression

arithmetic_expression ::= expression, arithmetic_operator, expression

list_expression ::= "[", "]" | "[", expression, {",", expression}, "]"

function_call ::= identifier, "(" (")" | expression, {",", expression}, ")")

expression ::= identifier | number | string | boolean | list_expression | "(", expression, ")" | arithmetic_expression | logical_expression | comparison_expression | function_call

evaluable ::= logical_expression | arithmetic_expression | assignment | definition | definition_and_assignment

body ::= "{", program, "}"

while_statement ::= ["please"], "while", "(", expression, ")", body

for_statement ::= ["please"], "for", "(", [evaluable], ";", [expression], ";", [expression], ")", body

iteration_statement ::= while_statement | for_statement

if_structure ::= "if", "(", expression, ")", body, ["else", (body | if_structure)]

if_statement ::= ["please"], if_structure

break_statement ::= "break", ["please"]

continue_statement ::= "continue", ["please"]

return_statement ::= "return", [expression], ["please"]

jump_statement ::= break_statement | return_statement | continue_statement

expression_statement ::= expression, ["please"]

initialization_statement ::= definition | definition_and_assignment | assignment

statement ::= iteration_statement | if_statement | jump_statement | function | expression_statement | initialization_statement

function ::= ["please"], ["thanks"], "fun", identifier, "(", (")" | identifier, {, ",", identifier}, ")"), body

comment ::= single_line_comment | multi_line_comment

single_line_comment ::= "//", {any symbol}, (newline | eof)

multi_line_comment ::= "/*", {any symbol}, "*/

program ::= "" | statement, {newline, statement}

start ::= program
